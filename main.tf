resource "aws_acm_certificate" "this" {
  count = "${var.create_certificate}"

  domain_name               = "${var.domain_name}"
  subject_alternative_names = ["${var.subject_alternative_names}"]
  validation_method         = "${var.validation_method}"

  tags = "${var.tags}"

  lifecycle {
    create_before_destroy = true
  }
}

# > Notice that ACM removes the wildcard label (`*`) when generating CNAME records for wildcard names. As a result, the CNAME record generated by ACM for a wildcard name (such as `*.example.com`) is the same record returned for the domain name without the wildcard label (`example.com`).
# https://aws.amazon.com/certificate-manager/faqs/#DNS_Validation_.28Public_Certificates.29
resource "aws_route53_record" "validation" {
  count = "${var.create_certificate && var.validation_method == "DNS" && var.validate_certificate ? length(local.filtered_subject_alternative_names) + 1 : 0}"

  zone_id = "${var.zone_id}"
  name    = "${lookup(aws_acm_certificate.this.domain_validation_options[count.index], "resource_record_name")}"
  type    = "${lookup(aws_acm_certificate.this.domain_validation_options[count.index], "resource_record_type")}"
  ttl     = 60
  records = ["${lookup(aws_acm_certificate.this.domain_validation_options[count.index], "resource_record_value")}"]
}

resource "aws_acm_certificate_validation" "this" {
  count = "${var.create_certificate && var.validation_method == "DNS" && var.validate_certificate && var.wait_for_validation ? 1 : 0}"

  certificate_arn = "${aws_acm_certificate.this.arn}"

  validation_record_fqdns = [
    "${aws_route53_record.validation.*.fqdn}",
  ]
}

# NOTE: Delete wildcard domain from the SANs list
locals {
  subject_alternative_names          = "${list(var.subject_alternative_names)}"
  wildcard_domain_name               = "${list("*.${var.domain_name}")}"
  list_with_item_in_front            = "${distinct(concat(local.wildcard_domain_name, local
  .subject_alternative_names))}"
  filtered_subject_alternative_names = "${slice(local.list_with_item_in_front, 1, length(local.list_with_item_in_front))}"
}
